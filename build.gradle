group 'io.github.aedans'
version '1.0.1'

buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply from: rootProject.file('gradle/generated-kotlin-sources.gradle')

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/kategory/maven' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'io.kategory:kategory-annotations:0.3.11'
    kapt    'io.kategory:kategory-annotations-processor:0.3.11'
}

// Codegen

int num = 26

task('codegen').doFirst {
    String out = sourceSets.main.kotlin.srcDirs.first().absolutePath + "/io/github/aedans/kop"
    Codegen.generateTypealiases(num, out)
    Codegen.generateFolds(num, out)
    Codegen.generateMaps(num, out)
    Codegen.generateValues(num, out)
}

compileKotlin.dependsOn codegen

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        publishToBintray(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['publishToBintray']
    publish = true

    pkg {
        repo = 'maven'
        name = 'kop'
        userOrg = user

        version {
            name = project.version
        }
    }
}
